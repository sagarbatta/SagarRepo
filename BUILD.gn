# Copyright (c) 2016 Qualcomm Technologies, Inc.
# All Rights Reserved.
# Confidential and Proprietary - Qualcomm Technologies, Inc.

if (is_android) {
  import("//build/buildflag_header.gni")
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")

  _target_arch = target_cpu
  _build_type = "Release"
  if (is_debug) {
    _build_type = "Debug"
  }

  copy("copy_libswewebrefiner") {
    sources = [ "$_target_arch/$_build_type/libswewebrefiner.so" ]
    outputs = [ "$root_shlib_dir/libswewebrefiner.so" ]
    data = [ "$root_shlib_dir/libswewebrefiner.so" ]
  }

  group("libswewebrefiner") {
    public_deps = [
      ":copy_libswewebrefiner",
      "//swe/swe_core:swe_core_native",
    ]
  }

  _wr_res_zip_path = "//components/web_refiner/java/libswewebrefiner_java.zip"
  _wr_unpacked_res_path = "//components/web_refiner/extracted/res/"

  _rebased_res_dir = rebase_path(_wr_unpacked_res_path, root_build_dir)
  _wr_res_dir_exists = exec_script("//build/dir_exists.py", [ _rebased_res_dir ], "string") == "True"
  if (!_wr_res_dir_exists) {
    exec_script("//build/makedirs.py", [ _rebased_res_dir ], "string")
  }

  _wr_extract_res_depfile = "//components/web_refiner/extracted/wr_extract_res.d"
  _wr_extract_res_stamp = "//components/web_refiner/extracted/wr_extract_res.stamp"

  extract_zip_args = [
    "--zip", rebase_path(_wr_res_zip_path, root_build_dir),
    "--exdir", rebase_path(_wr_unpacked_res_path, root_build_dir),
    "--depfile", rebase_path(_wr_extract_res_depfile, root_build_dir),
    "--stamp", rebase_path(_wr_extract_res_stamp, root_build_dir),
  ]
  exec_script("//components/web_refiner/extract_zip.py", extract_zip_args)

  android_resources("libswewebrefiner_java_resources") {
    custom_package = "com.qualcomm.qti.webrefiner"
    resource_dirs = [ _wr_unpacked_res_path ]
  }

  android_java_prebuilt("libswewebrefiner_java") {
    jar_path = "//components/web_refiner/java/libswewebrefiner_java.jar"
    deps = [
      ":libswewebrefiner_java_resources",
    ]
  }
}
